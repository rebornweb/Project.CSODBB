<html>
<head>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<title>FIM Errors/Reports</title>

<style type="text/css">
body      {background-color:wheat;}
table     {width:100%;font-family:Tahoma; font-size:70%;background-color:lightgrey;}
td.tbname {font-weight:bold; background-color:papayawhip;font-size:200%;}
td.tbhelp {font-weight:normal; background-color:papayawhip;}
td.head   {font-weight:bold; background-color:buttonface;}
td.data   {background-color:cornsilk;}
textarea  {font-size:70%;background-color:lightgrey;}
</style>

<script type="text/vbscript">
<!--
Option Explicit

Const AD_OPEN_FORWARD_ONLY = 0
Const AD_OPEN_STATIC = 3
Const AD_LOCK_READ_ONLY = 1
Const AD_LOCK_OPTIMISTIC = 3
Const AD_USE_CLIENT = 3
Const AD_USE_SERVER = 1

'Customizable parameters:

Const MAX_ROWS      = 30000
Const QUERY_TIMEOUT = 30
Const DATABASE_NAME = "FIMSynchronizationService"
Const CONN_STRING = "Provider=SQLOLEDB.1;Persist Security Info=True;Integrated Security=SSPI;Initial Catalog=xxx1;Data Source=xxx2;Timeout=30"

'Add the names of the tables or views you want to display here:
Dim tableNames
tableNames = array("Dynamic")

Sub Reload
    Dim oData
    Set oData = document.getElementById("DataArea")
    oData.InnerHTML = "<table ID=""dataTable""></table>"
    GetTableData()
End Sub

Sub GetTableData

   Dim oSQL, sqlServerName
   Set oSQL = document.getElementById("SQLServer")

   Dim tableName, tableData, oTable, oRow, oTbNameCell, oTbHelpCell, oCell, oReport, reportName, oRDN, rdn, oAccountName, accountName, optionElt, textNode
   Set oTable = document.getElementById("dataTable")

   Set oReport = document.getElementById("ReportName")
   reportName = oReport.Value

   Set oRDN = document.getElementById("RDN")
   rdn = oRDN.Value

   Set oAccountName = document.getElementById("AccountName")
   accountName = oAccountName.Value

   ' extract SQL and Help from common XML file
   Dim xmlDoc, xmlNod, sqlText, sqlHelp, optionName, optionValue
   Set xmlDoc = CreateObject("MSXML2.DOMDocument")
   xmlDoc.async = false
   sqlHelp = ""
   sqlText = ""
   If xmlDoc.Load("./AllSQL.xml") Then
        Set xmlNod = xmlDoc.documentElement.selectSingleNode("queries/query[@name='" & reportName & "']")
		If Not xmlNod Is Nothing Then
		    sqlHelp = Trim(xmlNod.selectSingleNode("help").text)
		    sqlText = Trim(xmlNod.selectSingleNode("sql").text)
            'sqlText = xmlNod.text
		End If

        ' construct report combo box
        If oReport.childNodes.length = 0 Then
            Set optionElt = document.createElement("option")
            optionElt.setAttribute "selected", "selected"
            optionElt.setAttribute "value", ""
            Set textNode = document.createTextNode("Select report ...")
            optionElt.appendChild textNode
            oReport.appendChild optionElt
            For Each xmlNod in xmlDoc.documentElement.selectNodes("queries/query")
                optionName = xmlNod.selectSingleNode("@name").Text
                Set optionElt = document.createElement("option")
                optionElt.setAttribute "value", optionName
                Set textNode = document.createTextNode(optionName)
                optionElt.appendChild textNode
                oReport.appendChild optionElt
            Next 
        End If

        ' construct server combo box
        If oSQL.childNodes.length = 0 Then
            For Each xmlNod in xmlDoc.documentElement.selectNodes("servers/server")
                optionName = xmlNod.Text
                optionValue = xmlNod.selectSingleNode("@value").Text
                Set optionElt = document.createElement("option")
                optionElt.setAttribute "value", optionValue
                Set textNode = document.createTextNode(optionName)
                optionElt.appendChild textNode
                oSQL.appendChild optionElt
            Next 
        End If
   End If
   Set xmlDoc = Nothing

   document.getElementById("SQLCommand").Value = sqlText

   If reportName = "" Then
     Exit Sub
   End If

   sqlServerName = oSQL.Value

   Dim objConnection, strConnection
   strConnection = Replace(Replace(CONN_STRING, "xxx2", sqlServerName), "xxx1", DATABASE_NAME)
   Set objConnection = CreateObject("ADODB.Connection")
   objConnection.ConnectionTimeout = QUERY_TIMEOUT
   objConnection.Open strConnection
   Dim objRecordset
   Set objRecordset = CreateObject("ADODB.Recordset")
   objRecordset.ActiveConnection = objConnection
   objRecordset.ActiveConnection.CommandTimeout = QUERY_TIMEOUT
   objRecordset.CursorLocation = AD_USE_CLIENT

   For Each tableName in tableNames
      'Set table header
      Set oRow              = oTable.insertRow()
      Set oTbNameCell       = oRow.insertCell() 
      oTbNameCell.ClassName = "tbname"
      oTbNameCell.InnerText = reportName 
      oTbNameCell.Align = "left"

      'Set table help
      Set oRow              = oTable.insertRow()
      Set oTbHelpCell       = oRow.insertCell() 
      oTbHelpCell.ClassName = "tbhelp"
      oTbHelpCell.InnerText = sqlHelp 
      oTbHelpCell.Align = "left"
     
      If tableName = "Dynamic" Then
        tableName = "( " & sqlText & ") X"
        If Len(rdn) > 0 And InStr(1,tableName,"@RDN@") >= 0 Then
            tableName = Replace(tableName, "@RDN@", rdn)
        End If
        If Len(accountName) > 0 And InStr(1,tableName,"@AccountName@") >= 0 Then
            tableName = Replace(tableName, "@AccountName@", accountName)
        End If
      End If
      objRecordset.Open "SELECT * FROM " & tableName, objConnection, _
                        AD_OPEN_FORWARD_ONLY, AD_LOCK_READ_ONLY
      
      If Not objRecordset.eof Then
         objRecordset.MoveFirst
         Set oRow   = oTable.insertRow()
         Dim curField
         For each curField in objRecordset.fields
            Set oCell  = oRow.insertCell() 
            oCell.ClassName = "head"
            oCell.InnerText =  Trim(curField.Name) 
         Next
        oTbNameCell.colSpan = objRecordset.fields.count
        oTbHelpCell.colSpan = objRecordset.fields.count
      End If
  
      Dim rowCounter 
      rowCounter = 0
      Do While Not objRecordset.eof
         Set oRow   = oTable.insertRow()
         For each curField in objRecordset.fields
            Set oCell  = oRow.insertCell() 
            oCell.ClassName = "data"
            oCell.InnerText =  Trim(curField) 
         Next
         objRecordset.MoveNext

         rowCounter = rowCounter + 1
         If rowCounter = MAX_ROWS Then
            Exit Do
         End If
      Loop

		' Add row count row
		Set oRow   = oTable.insertRow()
		Set oCell  = oRow.insertCell() 
		oCell.ClassName = "head"
		oCell.InnerText =  "Total Records: " & CStr(rowCounter)
		oCell.colSpan = objRecordset.fields.count

      objRecordset.Close
   Next
   objConnection.Close
End Sub

Sub CopyHTML()
    Dim oTable
    Set oTable = document.getelementbyid("DataArea")
    
    If(oTable Is Nothing) then 
       MsgBox "There is no data displayed", vbExclamation, oMyApp.applicationName
       Exit Sub
    End If

    window.clipboardData.SetData "text", oTable.outerHtml
  
    MsgBox "HMTL data copied to clipboard successfully", vbInformation, oMyApp.applicationName

 End Sub

Sub Quitsub
     self.close()
End Sub

-->
</script>

<HTA:APPLICATION ID="oMyApp"
APPLICATIONNAME="SQLTableViewer"
APPLICATION="yes"
CAPTION="yes"
SINGLEINSTANCE="yes"
ICON="unifyico1.ico"
SYSMENU="yes">
</HTA:APPLICATION>
</head>

<body onLoad="GetTableData">
<table border="0" width="50%">
    <tbody>
        <tr>
            <td align="right">Server:</td>
            <td colspan="1">
                <select id="SQLServer" name="SQLServer"></select>
            </td>
        </tr>
        <tr>
            <td align="right">Report:</td>
            <td colspan="1">
                <select id="ReportName" name="ReportName"></select>
            </td>
        </tr>
        <tr>
            <td align="right">RDN:</td>
            <td colspan="1">
                <input id="RDN" name="RDN" type="text" size="50" title="For use with AccountName-centric reports only" />
            </td>
        </tr>
        <tr>
            <td align="right">AccountName:</td>
            <td colspan="1">
                <input id="AccountName" name="AccountName" type="text" size="50" title="For use with AccountName-centric reports only" />
            </td>
        </tr>
        <tr>
            <td />
            <td colspan="1">
                <input id="Search" type="button" value=" Search " name="Search"  onClick="Reload">
                <input id="Copy HTML" type="button" value=" Copy HTML " name="Copy HTML"  onClick="CopyHTML">
                <input id="Quitbutton" type="button" value="  Quit  " name="Quit_button"  onClick="QuitSub">
            </td>
        </tr>
    </tbody>
</table>
<p>
    <span id="DataArea">
        <table ID="dataTable"></table>
    </span>
</p>
SQL:
<br />
<textarea name"SQLCommand" id="SQLCommand" readonly="readonly" rows="10" cols="130"></textarea>
</body>
</html>
