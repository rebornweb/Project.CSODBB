<html>
	<head>
		<title>employeeNumber</title>
		<HTA:APPLICATION 
		     ID="employeeNumber" 
		     APPLICATIONNAME="employeeNumber""
		     SCROLL="yes"
		     SINGLEINSTANCE="yes"
		>
	</head>


<SCRIPT LANGUAGE="VBScript">

Dim strDistinguishedName
Dim StremployeeNumber, strCN
Dim displayline
Dim objConnection, objCommand


' To re-configure this HTA for a different domain, change the following constant:

CONST strLDAPRoot = "LDAP://dc=dbb,dc=local"

Sub window_onload
''arrcommand = Split(employeeNumber.commandline," ")
''username.value = arrcommand(2)
''If username.value <> "" Then 
''   CheckUser
''End If 
End sub    

Sub printline(linetoprint)
	' called for messages displaying outcomes
	Dim saveline
    saveline = DataArea.InnerHTML
    displayline = "<font color='White' >"  & _
        saveline & "<br>" & FormatDateTime(Now(),3) & " " & linetoprint
    DataArea.InnerHTML =  displayline 
    saveline = displayline
End sub  
     
Sub CheckUser
	' taking a logon, check a/d for the distinguished name of the user, load the emp id
	' tec box with the current id if present
	Const ADS_SCOPE_SUBTREE = 2
	Dim strUserName
	
	strUserName = username.value
	dtStart = TimeValue(Now())
	
	Set objConnection = CreateObject("ADODB.Connection")
	Set objCommand =   CreateObject("ADODB.Command")
	objConnection.Provider = "ADsDSOObject"
	objConnection.Open "Active Directory Provider"
	Set objCommand.ActiveConnection = objConnection
	
	'objCommand.Properties("Cache Results") = False
	objCommand.Properties("Page Size") = 1000
	objCommand.Properties("Searchscope") = ADS_SCOPE_SUBTREE 
	
	objCommand.CommandText = _
	    "SELECT distinguishedName, employeeNumber, sAMAccountName, cn FROM '" & strLDAPRoot & "' WHERE objectCategory='user' " & _
	        "AND sAMAccountName='" & strUserName & "'"
	Set objRecordSet = objCommand.Execute
	 
	If objRecordset.RecordCount  = 0 Then
	    printline "AccountName: " & strUserName & " does not exist."
	Else
	   Do Until objRecordSet.EOF 
	          printline "DN: " & objRecordSet.Fields("distinguishedName").Value & _
	          " ID: <" & objRecordSet.Fields("employeeNumber").Value & ">" & _
	          " sAMAccountName: [" & objRecordSet.Fields("sAMAccountName").Value & "]"
	   strDistinguishedName = objRecordSet.Fields("distinguishedName").Value
	   StremployeeNumber = objRecordSet.Fields("employeeNumber").Value
	   semployeeNumber.Value =  StremployeeNumber
	   sCN.Value =  Replace(objRecordSet.Fields("cn").Value, "\,", ",")
	   objRecordset.MoveNext
	   Loop 
	End If
	 
	objConnection.Close
	Set objCommand = Nothing
	Set objConnection = Nothing
	
End Sub

Sub CheckCN

	Const ADS_SCOPE_SUBTREE = 2
	strCN = Replace(sCN.value, ",", "\,")
	
	Set objConnection = CreateObject("ADODB.Connection")
	Set objCommand =   CreateObject("ADODB.Command")
	objConnection.Provider = "ADsDSOObject"
	objConnection.Open "Active Directory Provider"
	Set objCommand.ActiveConnection = objConnection
	
	'objCommand.Properties("Cache Results") = False
	objCommand.Properties("Page Size") = 1000
	objCommand.Properties("Searchscope") = ADS_SCOPE_SUBTREE 
	
	objCommand.CommandText = _
	    "SELECT distinguishedName, employeeNumber, sAMAccountName, cn FROM '" & strLDAPRoot & "' WHERE objectCategory='user' " & _
	        "AND cn='" & strCN & "'"
	Set objRecordSet = objCommand.Execute
	 
	If objRecordset.RecordCount  = 0 Then
	    printline "CN: " & strCN & " does not exist."
	Else
	   Do Until objRecordSet.EOF 
	          printline "DN: " & objRecordSet.Fields("distinguishedName").Value & _
	          " ID: <" & objRecordSet.Fields("employeeNumber").Value & ">" & _
	          " sAMAccountName: [" & objRecordSet.Fields("sAMAccountName").Value & "]"
	   strDistinguishedName = objRecordSet.Fields("distinguishedName").Value
	   StremployeeNumber = objRecordSet.Fields("employeeNumber").Value
	   strCN = Replace(objRecordSet.Fields("cn").Value, "\,", ",")
	   semployeeNumber.Value =  StremployeeNumber
	   username.value = objRecordSet.Fields("sAMAccountName").Value
	   sCN.Value =  strCN
	   objRecordset.MoveNext
	   Loop 
	End If
	 
	objConnection.Close
	Set objCommand = Nothing
	Set objConnection = Nothing

End Sub

Sub CheckID
	' taking a logon, check a/d for the employeeNumber of the user, 
	' Since multiples can return, we will let the loop drop out with the last, 
	' the operator can go figure
	'

	Const ADS_SCOPE_SUBTREE = 2
	StremployeeNumber = semployeeNumber.value
	
	Set objConnection = CreateObject("ADODB.Connection")
	Set objCommand =   CreateObject("ADODB.Command")
	objConnection.Provider = "ADsDSOObject"
	objConnection.Open "Active Directory Provider"
	Set objCommand.ActiveConnection = objConnection
	
	'objCommand.Properties("Cache Results") = False
	objCommand.Properties("Timeout") = 30
	objCommand.Properties("Page Size") = 1000
	objCommand.Properties("Searchscope") = ADS_SCOPE_SUBTREE 
	
	objCommand.CommandText = _
	    "SELECT distinguishedName, employeeNumber, sAMAccountName, cn FROM '" & strLDAPRoot & "' WHERE objectCategory='user' " & _
	        "AND employeeNumber='" & StremployeeNumber & "'"
	Set objRecordSet = objCommand.Execute
	 
	If objRecordset.RecordCount  = 0 Then
	    printline "Employee id: " & StremployeeNumber & " does not exist."
	Else
	   Do Until objRecordSet.EOF 
		PrintRecord (objRecordSet)
'	          printline "DN: " & objRecordSet.Fields("distinguishedName").Value & _
'	          " ID: <" & objRecordSet.Fields("employeeNumber").Value & ">" & _
'	          " sAMAccountName: [" & objRecordSet.Fields("sAMAccountName").Value & "]"
	   	strDistinguishedName = objRecordSet.Fields("distinguishedName").Value
	  	StremployeeNumber = objRecordSet.Fields("employeeNumber").Value
	   	semployeeNumber.Value =  objRecordSet.Fields("employeeNumber").Value
	   	username.value = objRecordSet.Fields("sAMAccountName").Value
	   	sCN.Value =  Replace(objRecordSet.Fields("cn").Value, "\,", ",")
	   	objRecordset.MoveNext
	   Loop 
	End If
	 
	objConnection.Close
	Set objCommand = Nothing
	Set objConnection = Nothing
	
End Sub

Sub PrintRecord(ByVal objRecordSet)
	Dim i, j, strPrint
	strPrint = ""
	For i = 0 To objRecordset.Fields.Count - 1
	    if Len(strPrint) > 0 then
		strPrint = strPrint & ", "  
	    end if
	    strPrint = strPrint & objRecordset.Fields(i).Name & ": " 
	    ' For Multi Value attribute
	    If objRecordset.Fields(i).Type = adVariant And Not (IsNull(objRecordset.Fields(i).Value)) Then

	        For j = LBound(objRecordset.Fields(i).Value) To UBound(objRecordset.Fields(i).Value)
				strPrint = strPrint & objRecordset.Fields(i).Value(j)
	        Next 'j
	    Else
	        ' For Single Value attribute.
		strPrint = strPrint & CStr( objRecordset.Fields(i).Value )
	    End If
	    ' printline strPrint
	Next 'i
	printline strPrint
End Sub


Sub SetemployeeNumber
	'Update the current user name with the employeeNumber from the textbox
	
	CONST vbOKOnly = 0
	CONST vbOKCancel = 1
	CONST vbOK = 1
	CONST vbCancel = 2


	If MsgBox("Updating employeeNumber to " & semployeeNumber.value & ".  Press OK to continue", vbOKCancel) = vbOK Then
		User = username.value
		StremployeeNumber = semployeeNumber.value
		strldap = "LDAP://" & strDistinguishedName 
		Set objUser = GetObject (strldap)
		objUser.Put "employeeNumber",StremployeeNumber
		objUser.SetInfo   
		set oSession = nothing
		printline "DONE!"
	End If
End Sub 

Sub Quitsub
     self.close()
End Sub

Sub ResetAll
     username.Value = ""
     semployeeNumber.Value = ""
     sCN.Value = ""
     DataArea.InnerHTML = ""
End Sub

</SCRIPT>

<!-- ' PUT THE HTML TAGS HERE -->
	<body STYLE="font:10pt arial; color:white;
	 filter:progid:DXImageTransform.Microsoft.Gradient
	(GradientType=1, StartColorStr='#3399FF', EndColorStr='#0066CC')">
	
		<p>This HTA is to overwrite the employeeNumber attribute for an existing AD Account.</p>
		<p>It will</p>
		<ul>
			<li> Retrieive the user And employeeNumber from Logon name</li>
			<li> Or, Retrieve the last user With a given employeeNumber, listing all as it goes</li>
			<li> Then Allow you To Set the employeeNumber for that user</li>
		</ul>
		<p>
			<b><font color='red' face='Arial'>You need update rights to the target user to run this procedure</font></b>
		</p>
		<p>
			Enter AD Username: <input type="text" name="username" size = "30" />
			<input id=Checkuser  type="button" value="Find By sAMAccountName" name="Find By sAMAccountName"  onClick="Checkuser" />
		<p>
			employeeNumber: <input type="text" name="semployeeNumber" size = "10" />
			<input id=btnCheckEmplid  type="button" value="Find by employeeNumber" name="Find by employeeNumber"  onClick="Checkid" />
			<input id=btnSetemployeeNumber type="button" value="  Set employeeNumber" name="Set employeeNumber"  onClick="SetemployeeNumber" />
		<p>
		<p>
			CN: <input type="text" name="sCN" size = "30" />
			<input id=btnCheckEmplCN  type="button" value="Find by CN" name="Find by CN"  onClick="CheckCN" />
		<p>
		<input id="btnResetAll"  type="button" value="  Reset  " name="Reset"  onClick="ResetAll" />
		<input id=btnQuitbutton type="button" value="  Quit  " name="Quit_button"  onClick="QuitSub" />
		<span id="DataArea"></span>
	</body>
</html>
